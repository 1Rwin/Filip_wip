buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.27.0"
    id "org.sonarqube" version "2.8"
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

allprojects {
    group = 'com.schenker.poc'
    version = '0.1.0'
    sourceCompatibility = '11'
}

sonarqube {

    properties {
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.projectName", "PoC: One task at a time"
        property "sonar.tests", "src/test/groovy,src/test/java"
    }

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    configurations {
        groovy
        logback
        lombok
        springBootFramework
        springContext
        testLibs
    }

    dependencies {
        groovy "org.codehaus.groovy:groovy-all:${groovyVersion}"
        
        logback "ch.qos.logback:logback-classic:${logbackVersion}"
        
        lombok "org.projectlombok:lombok"

        springBootFramework "org.springframework.boot:spring-boot-starter"
        springBootFramework "org.springframework.boot:spring-boot-starter-data-jpa"
        springBootFramework "org.springframework.boot:spring-boot-starter-web"

        springContext "org.springframework:spring-context"

        testLibs "org.springframework.boot:spring-boot-starter-test"

        implementation configurations.lombok
        annotationProcessor configurations.lombok
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.2.2.RELEASE")
        }
    }
}
